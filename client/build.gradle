/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
    id "io.freefair.lombok"
    id 'jacoco'
    id 'io.github.goooler.shadow' version "8.1.7"
    id 'maven-publish'
    id 'signing'
}

dependencies {
    implementation project(path: ":${rootProject.name}-spi", configuration: 'shadow')
    implementation project(path: ":${rootProject.name}-common", configuration: 'shadow')
    compileOnly group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'

}

jacocoTestReport {
    reports {
        xml.getRequired().set(true)
        csv.getRequired().set(false)
        html.getRequired().set(true)
    }

    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.8
            }
        }
    }
    dependsOn jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification

shadowJar {
    archiveClassifier.set(null)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
    dependsOn javadoc
}

publishing {
    repositories {
        maven {
            name = 'staging'
            url = "${rootProject.buildDir}/local-staging-repo"
        }
        maven {
            name = "Snapshots"
            url = "https://central.sonatype.com/repository/maven-snapshots/"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "OpenSearch Machine Learning Client"
                packaging = "jar"
                url = "https://github.com/opensearch-project/ml-commons"
                description = "OpenSearch Machine Learning Client"
                scm {
                    connection = "scm:git@github.com:opensearch-project/ml-commons.git"
                    developerConnection = "scm:git@github.com:opensearch-project/ml-commons.git"
                    url = "git@github.com:opensearch-project/ml-commons.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "OpenSearch"
                        url = "https://github.com/opensearch-project/ml-commons"
                    }
                }
                // Add a dependencies section to signal that consumers of the client need to provide these
                withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    
                    def addProvidedDependency = { group, artifact, version ->
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', group)
                        dependency.appendNode('artifactId', artifact)
                        dependency.appendNode('version', version)
                        dependency.appendNode('scope', 'provided')
                    }

                    addProvidedDependency('org.json', 'json', '20231013')
                    addProvidedDependency('com.jayway.jsonpath', 'json-path', '2.9.0')
                    addProvidedDependency('org.apache.commons', 'commons-text', '1.10.0')
                }
            }
        }

    }

}
compileJava.dependsOn(':opensearch-ml-common:shadowJar')
delombok.dependsOn(':opensearch-ml-common:shadowJar')
