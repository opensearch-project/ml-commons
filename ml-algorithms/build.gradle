/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id "io.freefair.lombok"
    id 'com.diffplug.spotless' version '6.25.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ":${rootProject.name}-spi", configuration: 'shadow')
    implementation project(path: ":${rootProject.name}-common", configuration: 'shadow')
    implementation project(':opensearch-ml-memory')
    compileOnly group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    implementation "org.opensearch.client:opensearch-rest-client:${opensearch_version}"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    implementation "org.opensearch:common-utils:${common_utils_version}"
    implementation ("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}")
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    implementation group: 'org.tribuo', name: 'tribuo-clustering-kmeans', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-regression-sgd', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-anomaly-libsvm', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-classification-sgd', version: '4.2.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
    implementation 'software.amazon.randomcutforest:randomcutforest-parkservices:3.0-rc3'
    implementation 'software.amazon.randomcutforest:randomcutforest-core:3.0-rc3'
    implementation group: 'io.protostuff', name: 'protostuff-core', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-runtime', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-api', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-collectionschema', version: '1.8.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'
    implementation (group: 'com.google.guava', name: 'guava', version: '32.1.3-jre') {
	exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.11.0'
    implementation platform("ai.djl:bom:0.31.1")
    implementation group: 'ai.djl.pytorch', name: 'pytorch-model-zoo'
    implementation group: 'ai.djl', name: 'api'
    implementation group: 'ai.djl.huggingface', name: 'tokenizers'
    implementation("ai.djl.onnxruntime:onnxruntime-engine") {
        exclude group: "com.microsoft.onnxruntime", module: "onnxruntime"
    }
    // Multi-tenant SDK Client
    implementation "org.opensearch:opensearch-remote-metadata-sdk:${opensearch_build}"

    def os = DefaultNativePlatform.currentOperatingSystem
    //arm/macos doesn't support GPU
    if (os.macOsX || System.getProperty("os.arch") == "aarch64") {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime:1.16.3!!"
        }
    } else {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime_gpu:1.16.3!!"
        }
    }

    implementation platform('software.amazon.awssdk:bom:2.30.18')
    api 'software.amazon.awssdk:auth:2.30.18'
    implementation 'software.amazon.awssdk:apache-client'
    implementation ('com.amazonaws:aws-encryption-sdk-java:2.4.1') {
        exclude group: 'org.bouncycastle', module: 'bcprov-ext-jdk18on'
    }
    // needed by aws-encryption-sdk-java
    implementation "org.bouncycastle:bc-fips:${versions.bouncycastle_jce}"
    compileOnly group: 'software.amazon.awssdk', name: 'aws-core', version: "2.30.18"
    compileOnly group: 'software.amazon.awssdk', name: 's3', version: "2.30.18"
    compileOnly group: 'software.amazon.awssdk', name: 'regions', version: "2.30.18"

    implementation ('com.jayway.jsonpath:json-path:2.9.0') {
        exclude group: 'net.minidev', module: 'json-smart'
    }
    implementation('net.minidev:json-smart:2.5.2')
    implementation group: 'org.json', name: 'json', version: '20231013'
    implementation group: 'software.amazon.awssdk', name: 'netty-nio-client', version: "2.30.18"
    api('io.modelcontextprotocol.sdk:mcp:0.9.0')
    testImplementation("com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:${versions.jackson_databind}")
    testImplementation group: 'com.networknt' , name: 'json-schema-validator', version: '1.4.0'
}

lombok {
    version = "1.18.38"
}

configurations.all {
    resolutionStrategy.force 'com.google.protobuf:protobuf-java:3.25.5'
    resolutionStrategy.force 'org.apache.commons:commons-compress:1.26.0'
    resolutionStrategy.force 'software.amazon.awssdk:bom:2.30.18'
}

jacocoTestReport {
    reports {
        xml.getRequired().set(true)
        csv.getRequired().set(false)
        html.getRequired().set(true)
    }

    dependsOn test
}

List<String> jacocoExclusions = [
        // TODO: add more unit test to meet the minimal test coverage.
        'org.opensearch.ml.engine.algorithms.agent.MLConversationalFlowAgentRunner'
]

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                excludes = jacocoExclusions
                minimum = 0.65 //TODO: increase coverage to 0.90
            }
            limit {
                counter = 'BRANCH'
                excludes = jacocoExclusions
                minimum = 0.55 //TODO: increase coverage to 0.85
            }
        }
    }
    dependsOn jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification
compileJava.dependsOn(':opensearch-ml-common:shadowJar')

spotless {
    java {
        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com'

        eclipse().withP2Mirrors(Map.of("https://download.eclipse.org/", "https://mirror.umd.edu/eclipse/")).configFile rootProject.file('.eclipseformat.xml')
    }
}
