/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id "io.freefair.lombok"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ":${rootProject.name}-spi", configuration: 'shadow')
    implementation project(path: ":${rootProject.name}-common", configuration: 'shadow')
    implementation project(':opensearch-ml-memory')
    compileOnly group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    implementation "org.opensearch.client:opensearch-rest-client:${opensearch_version}"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    implementation "org.opensearch:common-utils:${common_utils_version}"
    implementation ("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}")
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.14.0'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    implementation group: 'org.tribuo', name: 'tribuo-clustering-kmeans', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-regression-sgd', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-anomaly-libsvm', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-classification-sgd', version: '4.2.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
    implementation 'software.amazon.randomcutforest:randomcutforest-parkservices:3.0-rc3'
    implementation 'software.amazon.randomcutforest:randomcutforest-core:3.0-rc3'
    implementation group: 'io.protostuff', name: 'protostuff-core', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-runtime', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-api', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-collectionschema', version: '1.8.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'
    implementation (group: 'com.google.guava', name: 'guava', version: '32.1.3-jre') {
	exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }
    implementation group: 'com.google.code.gson', name: 'gson', version: "${versions.gson}"
    implementation platform("ai.djl:bom:0.31.1")
    implementation group: 'ai.djl.pytorch', name: 'pytorch-model-zoo'
    implementation group: 'ai.djl', name: 'api'
    implementation group: 'ai.djl.huggingface', name: 'tokenizers'
    implementation("ai.djl.onnxruntime:onnxruntime-engine") {
        exclude group: "com.microsoft.onnxruntime", module: "onnxruntime"
    }
    // Multi-tenant SDK Client
    implementation "org.opensearch:opensearch-remote-metadata-sdk:${opensearch_build}"
    implementation 'commons-beanutils:commons-beanutils:1.11.0'
    implementation "org.opensearch:opensearch-remote-metadata-sdk-ddb-client:${opensearch_build}"

    def os = DefaultNativePlatform.currentOperatingSystem
    //arm/macos doesn't support GPU
    if (os.macOsX || System.getProperty("os.arch") == "aarch64") {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime:1.16.3!!"
        }
    } else {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime_gpu:1.16.3!!"
        }
    }

    implementation platform(group: 'software.amazon.awssdk', name: 'bom', version:"${versions.aws}")
    api 'software.amazon.awssdk:auth:${versions.aws}'
    implementation group: 'software.amazon.awssdk', name:'apache-client', version: "${versions.aws}"
    implementation (group: 'software.amazon.awssdk', name: 'bedrockruntime', version: "${versions.aws}") {
        exclude group: 'io.netty'
    }
    implementation ('com.amazonaws:aws-encryption-sdk-java:2.4.1') {
        exclude group: 'org.bouncycastle', module: 'bcprov-ext-jdk18on'
    }
    // needed by aws-encryption-sdk-java
    implementation "org.bouncycastle:bc-fips:${versions.bouncycastle_jce}"
    compileOnly group: 'software.amazon.awssdk', name: 'aws-core', version: "${versions.aws}"
    compileOnly group: 'software.amazon.awssdk', name: 's3', version: "${versions.aws}"
    compileOnly group: 'software.amazon.awssdk', name: 'regions', version: "${versions.aws}"

    implementation ('com.jayway.jsonpath:json-path:2.9.0') {
        exclude group: 'net.minidev', module: 'json-smart'
    }
    implementation('net.minidev:json-smart:2.5.2')
    implementation group: 'org.json', name: 'json', version: '20231013'
    implementation group: 'software.amazon.awssdk', name: 'netty-nio-client', version: "${versions.aws}"
    api('io.modelcontextprotocol.sdk:mcp:0.12.1')
    testImplementation("com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:${versions.jackson_databind}")
    testImplementation group: 'com.networknt' , name: 'json-schema-validator', version: '1.4.0'
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-sse', version: '4.12.0'
}

lombok {
    version = "1.18.38"
}

configurations.all {
    resolutionStrategy.force 'com.google.protobuf:protobuf-java:3.25.5'
    resolutionStrategy.force 'org.apache.commons:commons-compress:1.26.0'
    resolutionStrategy.force group: 'software.amazon.awssdk', name:'bom', version:"${versions.aws}"
    resolutionStrategy.force group: 'software.amazon.awssdk', name:'sts', version:"${versions.aws}"
}

jacocoTestReport {
    reports {
        xml.getRequired().set(true)
        csv.getRequired().set(false)
        html.getRequired().set(true)
    }

    dependsOn test
}

List<String> jacocoExclusions = [
        // TODO: add more unit test to meet the minimal test coverage.
        'org.opensearch.ml.engine.algorithms.agent.MLConversationalFlowAgentRunner'
]

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                excludes = jacocoExclusions
                minimum = 0.65 //TODO: increase coverage to 0.90
            }
            limit {
                counter = 'BRANCH'
                excludes = jacocoExclusions
                minimum = 0.55 //TODO: increase coverage to 0.85
            }
        }
    }
    dependsOn jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification
compileJava.dependsOn(':opensearch-ml-common:shadowJar')
